/*
 * Copyright (C) 2020 Zac Sweers
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'org.jetbrains.kotlin.jvm' version "1.6.0"
  id 'org.jetbrains.kotlin.kapt' version "1.6.0"
  id 'com.google.devtools.ksp' version "1.6.0-1.0.1"
  id 'application'
  id 'com.diffplug.spotless' version "6.0.0"
  id "com.github.ben-manes.versions" version "0.39.0"
}

repositories {
  mavenCentral()
}

def compileArgs = ['-Xjsr305=strict', '-progressive']

compileKotlin {
  kotlinOptions {
    jvmTarget = "17"
    freeCompilerArgs += compileArgs
  }
}

compileTestKotlin {
  kotlinOptions {
    jvmTarget = "17"
    freeCompilerArgs += compileArgs
  }
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(17))
  }
}

sourceSets {
  main {
    java {
      srcDir("build/generated/source/kapt/main")
    }
  }
}

mainClassName = 'dev.zacsweers.UpdateReadmeKt'

// Fat jar configuration to run this as a standalone jar
// Configuration borrowed from https://stackoverflow.com/a/49284432/3323598
jar {
  manifest {
    attributes(
        'Main-Class': 'dev.zacsweers.UpdateReadmeKt'
    )
  }
  from {
    configurations.compileClasspath.filter { it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }
  }
}

dependencies {
  ksp 'dev.zacsweers.moshix:moshi-ksp:0.14.1'
  ksp 'dev.zacsweers.moshix:moshi-sealed-codegen:0.14.1'
  kapt 'com.tickaroo.tikxml:processor:0.8.13'

  implementation "com.github.ajalt:clikt:2.8.0"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2"
  implementation 'com.squareup.okio:okio:3.0.0'
  implementation "com.squareup.okhttp3:okhttp:5.0.0-alpha.2"
  implementation "com.squareup.retrofit2:retrofit:2.9.0"
  implementation "com.squareup.retrofit2:converter-moshi:2.9.0"
  implementation "com.squareup.moshi:moshi:1.12.0"
  implementation "com.squareup.moshi:moshi-adapters:1.12.0"
  implementation 'dev.zacsweers.moshix:moshi-sealed-runtime:0.14.1'

  // XML serialization
  implementation 'com.tickaroo.tikxml:annotation:0.8.13'
  implementation 'com.tickaroo.tikxml:core:0.8.13'
  implementation 'com.tickaroo.tikxml:retrofit-converter:0.8.13'
  implementation 'com.tickaroo.tikxml:converter-htmlescape:0.8.13'
}
