/*
 * Copyright (C) 2020 Zac Sweers
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'application'
  id 'org.jetbrains.kotlin.jvm' version "1.4.10"
  id 'org.jetbrains.kotlin.kapt' version "1.4.10"
  id 'com.diffplug.gradle.spotless' version "4.5.0"
}

repositories {
  mavenCentral()
}

def compileArgs = ['-Xjsr305=strict', '-progressive']

compileKotlin {
  kotlinOptions {
    jvmTarget = "14"
    freeCompilerArgs += compileArgs
  }
}

compileTestKotlin {
  kotlinOptions {
    jvmTarget = "14"
    freeCompilerArgs += compileArgs
  }
}

java {
  sourceCompatibility = JavaVersion.VERSION_14
  targetCompatibility = JavaVersion.VERSION_14
}

sourceSets {
  main {
    java {
      srcDir("build/generated/source/kapt/main")
    }
  }
}

// Idk why but this reports false positives
//apply from: rootProject.file('gradle/config-spotless.gradle')

mainClassName = 'dev.zacsweers.UpdateReadmeKt'

// Fat jar configuration to run this as a standalone jar
// Configuration borrowed from https://stackoverflow.com/a/49284432/3323598
jar {
  manifest {
    attributes(
        'Main-Class': 'dev.zacsweers.UpdateReadmeKt'
    )
  }
  from {
    configurations.compileClasspath.filter { it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }
  }
}

dependencies {
  kapt "com.squareup.moshi:moshi-kotlin-codegen:1.11.0"
  kapt "dev.zacsweers.moshix:moshi-sealed-codegen:0.4.0"
  kapt 'com.tickaroo.tikxml:processor:0.8.13'

  implementation "com.github.ajalt:clikt:2.7.1"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9"
  implementation "com.squareup.okio:okio:2.9.0"
  implementation "com.squareup.okhttp3:okhttp:4.9.0"
  implementation "com.squareup.retrofit2:retrofit:2.9.0"
  implementation "com.squareup.retrofit2:converter-moshi:2.9.0"
  implementation "com.squareup.moshi:moshi:1.11.0"
  implementation "com.squareup.moshi:moshi-adapters:1.11.0"
  implementation "dev.zacsweers.moshix:moshi-sealed-runtime:0.4.0"

  // XML serialization
  implementation 'com.tickaroo.tikxml:annotation:0.8.13'
  implementation 'com.tickaroo.tikxml:core:0.8.13'
  implementation 'com.tickaroo.tikxml:retrofit-converter:0.8.13'
  implementation 'com.tickaroo.tikxml:converter-htmlescape:0.8.13'

  testImplementation "junit:junit:4.13"
  testImplementation "com.google.truth:truth:1.0.1"
}
